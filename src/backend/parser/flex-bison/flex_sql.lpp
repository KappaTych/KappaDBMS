
Digit        [0-9]
Letter       [a-zA-Z_]
HexDigit     [a-fA-F0-9]
Exp          [Ee][+-]?{Digit}+
UNSIGN_INT   {Digit}+

%{
    #include "flex_sql.hpp"
    #include "bison_sql.hpp"
    #include "location.hh"

    #define YY_USER_ACTION loc.columns(yyleng);

    #define yyterminate() return yy::BisonParser::make_END_FILE(loc);

%}

%option outfile="flex_sql.cpp"

%option c++

%option noyywrap
%option case-insensitive
%option yyclass="FlexScanner"

%s COMMENT

%%

%{
    loc.step();
%}
--              BEGIN(COMMENT);
<COMMENT>[^\n]* ;
<COMMENT>\n     BEGIN(INITIAL);

[ \t]+      { loc.step(); }
\n+         { loc.lines(yyleng); loc.step(); }

CREATE      return yy::BisonParser::make_CREATE(loc);
DROP        return yy::BisonParser::make_DROP(loc);
SHOW        return yy::BisonParser::make_SHOW(loc);
TABLE       return yy::BisonParser::make_TABLE(loc);
NULL        return yy::BisonParser::make_NULL_(loc);
SELECT      return yy::BisonParser::make_SELECT(loc);
UPDATE      return yy::BisonParser::make_UPDATE(loc);
SET         return yy::BisonParser::make_SET(loc);
WHERE       return yy::BisonParser::make_WHERE(loc);
LIMIT       return yy::BisonParser::make_LIMIT(loc);
OFFSET      return yy::BisonParser::make_OFFSET(loc);
VALUES      return yy::BisonParser::make_VALUES(loc);
INTO        return yy::BisonParser::make_INTO(loc);
INSERT      return yy::BisonParser::make_INSERT(loc);
FROM        return yy::BisonParser::make_FROM(loc);
CAST        return yy::BisonParser::make_CAST(loc);
AS          return yy::BisonParser::make_AS(loc);
ESCAPE      return yy::BisonParser::make_ESCAPE(loc);
ISNULL      return yy::BisonParser::make_ISNULL(loc);
NOTNULL     return yy::BisonParser::make_NOTNULL(loc);
BETWEEN     return yy::BisonParser::make_BETWEEN(loc);
DISTINCT    return yy::BisonParser::make_DISTINCT(loc);
EXISTS      return yy::BisonParser::make_EXISTS(loc);
WHEN        return yy::BisonParser::make_WHEN(loc);
THEN        return yy::BisonParser::make_THEN(loc);
ELSE        return yy::BisonParser::make_ELSE(loc);

NOT         return yy::BisonParser::make_NOT(loc);
COLLATE     return yy::BisonParser::make_COLLATE(loc);

IS          return yy::BisonParser::make_IS(loc);
IN          return yy::BisonParser::make_IN(loc);
LIKE        return yy::BisonParser::make_LIKE(loc);
GLOB        return yy::BisonParser::make_GLOB(loc);
MATCH       return yy::BisonParser::make_MATCH(loc);
REGEXP      return yy::BisonParser::make_REGEXP(loc);
AND         return yy::BisonParser::make_AND(loc);
OR          return yy::BisonParser::make_OR(loc);

DOUBLE      return yy::BisonParser::make_DOUBLE(loc); 
TEXT        return yy::BisonParser::make_TEXT(loc);
INTEGER     return yy::BisonParser::make_INTEGER(loc);

"=="        return yy::BisonParser::make_EQUAL(loc);
"!="        return yy::BisonParser::make_NOTEQUAL(loc);
"<>"        return yy::BisonParser::make_NOTEQUAL(loc);
"<="        return yy::BisonParser::make_LESSEQUAL(loc);
">="        return yy::BisonParser::make_GREATERQUALL(loc);
"<"         return yy::BisonParser::make_LESS(loc);
">"         return yy::BisonParser::make_GREATER(loc);
"="         return yy::BisonParser::make_EQUAL(loc);

"||"        return yy::BisonParser::make_CONCATENATION(loc);

"~"         return yy::BisonParser::make_BINNOT(loc);
"&"         return yy::BisonParser::make_BINAND(loc);
"|"         return yy::BisonParser::make_BINOR(loc);
"<<"        return yy::BisonParser::make_LEFTSHIFT(loc);
">>"        return yy::BisonParser::make_RIGHTSHIFT(loc);

"+"         return yy::BisonParser::make_PLUS(loc);
"-"         return yy::BisonParser::make_MINUS(loc);
"*"         return yy::BisonParser::make_MULTIPLY(loc);
"/"         return yy::BisonParser::make_DIVISION(loc);
"%"         return yy::BisonParser::make_MOD(loc);

";"         return yy::BisonParser::make_SEMICOLON(loc);
"."         return yy::BisonParser::make_DOT(loc);
","         return yy::BisonParser::make_COMMA(loc);
"("         return yy::BisonParser::make_OPENBRACKET(loc);
")"         return yy::BisonParser::make_CLOSEBRACKET(loc);


{UNSIGN_INT} {
    return yy::BisonParser::make_INTEGER_CONST(std::stoll(yytext), loc);
}

({UNSIGN_INT}\.{UNSIGN_INT}?|{UNSIGN_INT}?\.{UNSIGN_INT}){Exp}? {
    return yy::BisonParser::make_DOUBLE_CONST(std::stold(yytext), loc);
}

\"[^\"\n]+\" {
    return yy::BisonParser::make_IDENTIFIER(std::string(yytext + 1, yyleng - 1), loc);
}

{Letter}({Letter}|{Digit})* {
    return yy::BisonParser::make_IDENTIFIER(std::string(yytext), loc);
}

\'([^'\n]|\'\')*\' {
    return yy::BisonParser::make_STRING(std::string(yytext + 1, yyleng - 1), loc);
}

. { 
    throw "[SQL-Lexer-Error] Unknown Character: " +  yytext[0];
}

%%

int yyerror(const char *msg) {
    throw "[SQL-Lexer-Error] " + std::string(msg);
}